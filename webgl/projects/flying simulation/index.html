<!DOCTYPE html>
<html>
<head>
	
	<script type="vert" id="vertex">
		attribute vec3 pos;
		attribute vec3 normal;
		attribute vec2 uv;

		varying vec3 Normal;
		varying vec2 Uv;
		varying vec3 FragPos;

		uniform mat4 projection;
		uniform mat4 model;
		uniform mat4 scale;
		uniform mat4 view;

		void main()
		{
			gl_Position = projection * view * model * scale * vec4(pos, 1.0);

			Normal  = vec3(model * vec4(normal, 0.0));
			Uv      = uv;
			FragPos = vec3(model * scale * vec4(pos, 1.0));

		}
	</script>
	<script type="frag" id="fragment">
		precision mediump float;

		varying vec3 Normal;
		varying vec2 Uv;
		varying vec3 FragPos;

		uniform sampler2D diffuse;
		uniform samplerCube skybox;
		uniform vec3 eyepos;

		void main()
		{
			vec3 norm = normalize(Normal);
			vec3 sunlight = normalize(vec3(5.0, 20.0, -5.0));
			float sunLightContrib = max(dot(sunlight, Normal), 0.0);

			vec2 uvs = Uv;

			vec4 diffusec = texture2D(diffuse, vec2(uvs.x, 1.0 - uvs.y));

			vec3 viewDir   = normalize(FragPos - eyepos);
			vec3 reflected = reflect(viewDir, Normal);
			vec4 skybox    = textureCube(skybox, reflected, 3.0);

			float fresnelRatio = max(dot(-viewDir, Normal), 0.0);
			if(fresnelRatio < 0.2)
			{
				float ratio = pow((fresnelRatio) / 0.2,  1.0);
				fresnelRatio = ratio;
			}
			else
				fresnelRatio = 1.0;

			//finalcolor
			diffusec += skybox * 0.2;
			diffusec.xyz /= 2.5;


			diffusec.xyz = diffusec.xyz * 0.3 + diffusec.xyz * 0.7 * sunLightContrib;

			gl_FragColor = diffusec * fresnelRatio + skybox * (1.0 - fresnelRatio);// * (1.0 + (1.0 - fresnelRatio));
		}
	</script>




	<script type="vert" id="skyboxvertex">
		attribute vec3 pos;

		uniform mat4 projection;
		uniform mat4 model;
		uniform mat4 view;

		varying vec3 Coord;

		void main()
		{
			vec4 multByViewModel = view * model * vec4(pos, 0.0);

			multByViewModel.w = 1.0;
			gl_Position = projection * multByViewModel;

			Coord = vec3(model * vec4(pos, 0.0));
		}
	</script>
	<script type="frag" id="skyboxfragment">
		precision mediump float;

		uniform samplerCube cubeMap;

		varying vec3 Coord;

		void main()
		{
			gl_FragColor = textureCube(cubeMap, Coord / 3.0);
		}
	</script>





	<script type="vert" id="terrainvertex">
		attribute vec3 pos;
		attribute vec3 normal;
		attribute vec3 tangents;
		attribute vec3 bitangents;
		attribute vec2 coord;

		varying vec3 Normal;
		varying vec2 Coord;
		varying vec3 FragPos;
		varying vec3 Eyepos;

		varying vec3 tangentEye;
		varying vec3 tangentFrag;

		varying vec3 vViewPosition;

		uniform vec3 eyepos;

		uniform mat4 projection;
		uniform mat4 model;
		uniform mat4 view;


		mat3 transpose(mat3 inMatrix) {

		    vec3 i0 = inMatrix[0];
		    vec3 i1 = inMatrix[1];
		    vec3 i2 = inMatrix[2];
		
		    mat3 outMatrix = mat3(
		                 vec3(i0.x, i1.x, i2.x),
		                 vec3(i0.y, i1.y, i2.y),
		                 vec3(i0.z, i1.z, i2.z)
		                 );
		
		    return outMatrix;
		}


		void main()
		{

			gl_Position = projection * view * model * 
			vec4(pos.x, pos.y /* 1000000.1*/, pos.z, 1.0);

			Normal = vec3(model * vec4(normal, 0.0));
			Coord = coord;

			FragPos = vec3(model * vec4(pos.x, pos.y/* / 1000000.1*/, pos.z, 1.0));
			Eyepos = eyepos;



			/*vec3 T = cross(normal, vec3(0.0, 1.0, 0.0));
			vec3 B = cross(normal, T);*/

			vec3 T   = normalize(mat3(model) * tangents);//*vec3(1.0, 0.0, 0. 0));
    		vec3 B   = normalize(mat3(model) * -bitangents); //*vec3(0.0, 0.0, -1.0));
    		vec3 N   = normalize(mat3(model) * normal);//* vec3(0.0, 1.0, 0.0));
    		mat3 TBN = transpose(mat3(T, B, N));

    		tangentEye  = TBN * Eyepos;
    		tangentFrag = TBN * FragPos;


    		vec4 mvPosition = view * model * vec4( pos, 1.0 );
			vViewPosition = -mvPosition.xyz;
		}
	</script>
	<script type="frag" id="terrainfragment">
		precision highp float;

		uniform sampler2D terrain;
		uniform samplerCube	skybox;
		uniform sampler2D depthmap;

		uniform vec3 aircraftpos;

		varying vec3 Normal;
		varying vec2 Coord;
		varying vec3 FragPos;
		varying vec3 Eyepos;

		varying vec3 tangentEye;
		varying vec3 tangentFrag;


		/*vec2 ParallaxMapping(vec2 texCoords, vec3 viewDir)
		{
			const float height_scale = 0.03;
			const float numLayers = 10.0;
    		// calculate the size of each layer
    		float layerDepth = 1.0 / numLayers;
    		// depth of current layer
    		float currentLayerDepth = 0.0;
    		// the amount to shift the texture coordinates per layer (from vector P)
    		vec2 P = viewDir.xy / viewDir.z * height_scale; 
    		vec2 deltaTexCoords = P / numLayers;


    		vec2  currentTexCoords     = texCoords;
			float currentDepthMapValue = 1.0 - texture2D(depthmap, currentTexCoords).r;
			  
			for(int i = 0; i < 11; i++)
			{
				 if(currentLayerDepth < currentDepthMapValue)
				 {
				 	// shift texture coordinates along direction of P
			    	currentTexCoords -= deltaTexCoords;
			    	// get depthmap value at current texture coordinates
			    	currentDepthMapValue = 1.0 - texture2D(depthmap, currentTexCoords).r;  
			    	// get depth of next layer
			    	currentLayerDepth += layerDepth; 
				 }
			}

			// get texture coordinates before collision (reverse operations)
			vec2 prevTexCoords = currentTexCoords + deltaTexCoords*1.0;
				
			// get depth after and before collision for linear interpolation
			float afterDepth  = currentDepthMapValue;// - currentLayerDepth;
			float beforeDepth = (1.0 - texture2D(depthmap, prevTexCoords).r);// - currentLayerDepth + layerDepth;
				 
			// interpolation of texture coordinates
			float weight = (afterDepth + beforeDepth) / 2.0;//afterDepth / (afterDepth - beforeDepth);
			vec2 finalTexCoords = prevTexCoords * weight + currentTexCoords * (1.0 - weight);
				
			return finalTexCoords;  
		}*/

		vec2 parallaxMap(in vec2 Coord, in vec3 V ) {

				// Determine number of layers from angle between V and N
				float parallaxScale = 0.006;
				float parallaxMaxLayers = 20.0;
				float parallaxMinLayers = 5.0;
				float numLayers = mix( parallaxMaxLayers, parallaxMinLayers, abs( dot( vec3( 0.0, 0.0, 1.0 ), V ) ) );

				float layerHeight = 1.0 / numLayers;
				float currentLayerHeight = 0.0;
				// Shift of texture coordinates for each iteration
				vec2 dtex = parallaxScale * V.xy / V.z / numLayers;

				vec2 currentTextureCoords = Coord;

				float heightFromTexture = 1.0 - texture2D( depthmap, currentTextureCoords ).r;

				// while ( heightFromTexture > currentLayerHeight )
				// Infinite loops are not well supported. Do a "large" finite
				// loop, but not too large, as it slows down some compilers.
				for ( int i = 0; i < 20; i += 1 ) {
					if ( heightFromTexture <= currentLayerHeight ) {
						break;
					}
					currentLayerHeight += layerHeight;
					// Shift texture coordinates along vector V
					currentTextureCoords -= dtex;
					heightFromTexture = 1.0 - texture2D( depthmap, currentTextureCoords ).r;
				}

				vec2 prevTCoords = currentTextureCoords + dtex;

				// Heights for linear interpolation
				float nextH = heightFromTexture - currentLayerHeight;
				float prevH =  1.0 - texture2D( depthmap, prevTCoords ).r - currentLayerHeight + layerHeight;

				// Proportions for linear interpolation
				float weight = nextH / ( nextH - prevH );

				// Interpolation of texture coordinates
				return prevTCoords * weight + currentTextureCoords * ( 1.0 - weight );
			}



		void main()
		{
			//per qualche motivo i normal blender me li ha fatti al contrario.. e vabbò
			vec3 norm = normalize(Normal);

			vec2 FinalCoord = 
			vec2(Coord.x * 10.50 + aircraftpos.x * 0.00059,
			     Coord.y * 10.50 - aircraftpos.z * 0.00059);


			vec3 viewDir = normalize(FragPos - Eyepos);
			float fresnelRatio = max(dot(-viewDir, norm), 0.0);
			vec3 reflected = reflect(viewDir, norm);

			


			vec4 diffusec = vec4(0.0);
			//l'altezza era sempre minore di 2000 bestia, l'aereo è scalatissimo
			//l'altezza era sempre minore di 2000 bestia, l'aereo è scalatissimo
			//l'altezza era sempre minore di 2000 bestia, l'aereo è scalatissimo
			//l'altezza era sempre minore di 2000 bestia, l'aereo è scalatissimo
			//texture2D(terrain, FinalCoord);

			for(int i = 1; i < 3; i++) {
				vec3 viewVector = normalize(tangentEye - tangentFrag);
				float multiplier = length(FragPos - Eyepos) / 500.0;
				float displace = pow(1.0 - max(dot(norm, -viewDir), 0.0),  32.0) * 0.5 * multiplier;


				vec2 coordss = parallaxMap( FinalCoord, vec3(viewVector.x, viewVector.y + displace*float(i), viewVector.z));
				diffusec += texture2D(terrain, coordss);

				 coordss = parallaxMap( FinalCoord, vec3(viewVector.x, viewVector.y - displace*float(i), viewVector.z));
				diffusec += texture2D(terrain, coordss);

				 coordss = parallaxMap( FinalCoord, vec3(viewVector.x + displace*float(i), viewVector.y, viewVector.z));
				diffusec += texture2D(terrain, coordss);

				 coordss = parallaxMap( FinalCoord, vec3(viewVector.x - displace*float(i), viewVector.y, viewVector.z));
				diffusec += texture2D(terrain, coordss);
			}
			diffusec /= 4.0 * 2.0; 

			float sunLightContrib = max(dot(norm, normalize(vec3(1.0, 5.0, -1.0))), 0.0);

			float powerValue = 32.0;
			if(sunLightContrib < 0.967) {	
				
				float ratio = 1.0 - sunLightContrib / 0.967;
				ratio = pow(ratio, 0.9);
				powerValue = 1500.0 * ratio  + 32.0;

				//sunLightContrib = 0.0;
			}

			sunLightContrib = pow(sunLightContrib, powerValue);
			sunLightContrib = pow(sunLightContrib, 1.5);
			vec3 sunLightColor = vec3(1.0, 0.7, 1.0);


			//vec4 diffusec = texture2D(terrain, FinalCoord);
			vec4 Reflection = textureCube(skybox, reflected, 4.3);

			if(fresnelRatio < 0.1)
			{
				float ratio = (fresnelRatio / 0.1);
				fresnelRatio = ratio;
			}
			else
				fresnelRatio = 1.0;


			diffusec.xyz   *= fresnelRatio;

			float redChannel = pow(sunLightContrib, 0.1);
			if(redChannel > 1.0) redChannel = 1.0;
			if(redChannel < 0.7) redChannel = 0.7;
			diffusec   *= vec4(redChannel, 0.7, 1.0, 1.0);

			float visibility = pow(sunLightContrib, 0.1);
			if(visibility < 0.8) visibility = 0.8;
			diffusec.xyz *= visibility * 1.0;


			/*sunspecularity*/
			vec3 sunDir = normalize(vec3(50.0, 5.0, -50.0));
			vec3 h      = normalize(-viewDir + sunDir);
			float specContr = pow(max(dot(h, norm), 0.0), 8.0) / 2.0;
			/*sunspecularity - END*/


			gl_FragColor = diffusec + 
							vec4(Reflection.xyz * (1.0 - fresnelRatio), 1.0) + 
							Reflection * (specContr * 1.3) * diffusec;/* + vec4(sunLightColor * sunLightContrib * 1.0 * 1.0, 1.0)*/;
		}
	</script>





	<script type="vert" id="godrayvertex">
		attribute vec3 pos;
		attribute vec3 normal;
		attribute vec2 uv;

		uniform float pass;

		uniform mat4 projection;
		uniform mat4 model;
		uniform mat4 scale;
		uniform mat4 view;

		varying vec3 Pos;
		varying float Pass;

		void main()
		{
			if(pass > 1.0) {
				gl_Position = projection * view * model * scale * vec4(pos, 1.0);
			}
			else {
				vec4 multByViewModel = view * model * vec4(pos, 0.0);

				multByViewModel.w = 1.0;
				gl_Position = projection * multByViewModel;

				Pos = vec3(model * vec4(pos, 0.0));
			}

			normal;
			uv;
			Pass = pass;
		}
	</script>
	<script type="frag" id="godrayfragment">
		precision mediump float;
		
		varying vec3 Pos;
		varying float Pass;

		uniform samplerCube skybox;
		uniform vec3 cameraForward;

		void main()
		{
			if(Pass > 1.0)
			{
				//aircraft pass
				gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0);
			}
			else
			{
				float dotWithCamera = 

				pow(max(dot(-cameraForward, normalize(vec3(5.0, 0.63, -5.0))), 0.0), 10.0);




				//cubemap pass
				gl_FragColor = vec4(textureCube(skybox, Pos).xyz * dotWithCamera * 0.4, 1.0);

				/*gl_FragColor = vec4(71.0 / 255.0 * dotWithCamera,  
									34.0 / 255.0 * dotWithCamera,  
									31.0 / 255.0 * dotWithCamera,  
									1.0);*/

				if(dot(normalize(Pos), normalize(vec3(5.0, 0.63, -5.0))) > 0.9999)
				{
					gl_FragColor = vec4(255.0 / 255.0 * dotWithCamera, 
										114.0 / 255.0 * dotWithCamera, 
										104.0 / 255.0 * dotWithCamera, 
										1.0);
				}
			}
		}
	</script>






	<script type="vert" id="radialblurvertex">
		attribute vec3 pos;

		uniform mat4 projection;
		uniform mat4 model;
		uniform mat4 view;

		varying vec2 coord;
		varying vec4 sunProjCo;

		void main()
		{
			sunProjCo = projection * view * vec4(50000.0, 6300, -50000, 1.0);

			sunProjCo /= sunProjCo.w;
			sunProjCo.x = sunProjCo.x * 0.5 + 0.5;
			sunProjCo.y = sunProjCo.y * 0.5 + 0.5;

			gl_Position = vec4(pos, 1.0);
			coord = vec2(pos.x * 0.5 + 0.5, 
				         pos.y * 0.5 + 0.5);
		}
	</script>
	<script type="frag" id="radialblurfragment">
		precision mediump float;

		uniform sampler2D texture;

		varying vec2 coord;
		varying vec4 sunProjCo;

		void main()
		{
			/*if(sunProjCo.x > 1.0) //z < -1.0)
			{
				gl_FragColor = vec4(1.0, 0.0, 0.0, 1.0);
				return;
				//discard;
			}*/

			//radial blur center
			vec2 tcoords = sunProjCo.xy;
			if(tcoords.y > 1.0)
			{
				tcoords.x = tcoords.x / tcoords.y;
				tcoords.y = 1.0;
			}

			if(tcoords.x > 1.0)
			{
				tcoords.y = tcoords.y / tcoords.x;
				tcoords.x = 1.0;
			}


			float numsamples = 10.0;
			vec2 distance = (coord - tcoords) * 0.065;
			vec2 sample = distance / numsamples;



			vec4 col  = texture2D(texture, coord);
			for(int i = 1; i < 60; i++)
			{
				col += texture2D(texture, coord - sample * float(i));
			}

			gl_FragColor = col / 60.0;//numsamples;
		}
	</script>



	<script type="vert" id="displayquadvertex">
		attribute vec3 pos;

		varying vec2 coord;

		void main()
		{
			gl_Position = vec4(pos, 1.0);
			coord = vec2(pos.x * 0.5 + 0.5, pos.y * 0.5 + 0.5);
		}
	</script>
	<script type="frag" id="displayquadfragment">
		precision mediump float;

		uniform sampler2D texture;
		uniform sampler2D unblurred;

		varying vec2 coord;
		void main()
		{
			//gl_FragColor = texture2D(texture, coord);
			vec4 original = vec4(texture2D(unblurred, coord).xyz * 1.0, 1.0);
			vec4 godRays  = vec4(texture2D(texture, coord).xyz * 0.9, 1.0);

			vec4 finalColor = original + godRays;
         

			gl_FragColor = finalColor;
		}
	</script>





	<script type="text/javascript" src="fx-min.js"></script>

	<style>

	* {
		margin: 0;
	}

	html, body {
		overflow: hidden;
		width: 100%;
		height: 100%;
		background: #303030;
	}

	#speed {
		position: absolute;
		bottom:0;
		right:0;
		margin: 10px;
		font-size: 1.3em;
		color: rgba(250, 250, 250, .75);
		font-family: consolas;
	}

	</style>

</head>


<body onload="webGLStart();">

	<canvas id="canvas" style="border: none;"></canvas>

	<p style="position: absolute; bottom: 2vh; left: 2vh; font-size: 1.2em; color: white;"> arrows to move, shift to accelerate </p>

</body>

</html>